###############################################################################
# CMake config file for generalized_lazy_search
#
# This sets the following variables:
# @PROJECT_NAME@_INCLUDE_DIRS - Directories containing the generalized_lazy_search include files.
# @PROJECT_NAME@_LIBRARIES    - Libraries needed to use generalized_lazy_search.
# @PROJECT_NAME@_VERSION      - generalized_lazy_search version.
###############################################################################

@PACKAGE_INIT@

function(generalized_lazy_search_traverse_components output_variable)
  set(output_components)

  foreach(component ${ARGN})
    # We should be able to eliminate the <C>_VISITED property by detecting
    # whether the <C>_FOUND property is DEFINED. Unfortunately, DEFINED checks
    # whether or not define_property() was called, not whether the variable was
    # set. The exact behavior seems to vary between CMake versions, so we use
    # an extra variable instead.
    get_property(is_visited DIRECTORY PROPERTY "generalized_lazy_search_${component}_VISITED")
    set_property(DIRECTORY PROPERTY "generalized_lazy_search_${component}_VISITED" TRUE)

    if(NOT is_visited)
      set(component_include
        "${CMAKE_CURRENT_LIST_DIR}/generalized_lazy_search_${component}Component.cmake")
      set(target_include
        "${CMAKE_CURRENT_LIST_DIR}/generalized_lazy_search_${component}Targets.cmake")

      if(EXISTS "${component_include}" AND EXISTS "${target_include}")
        include("${component_include}")

        set_property(DIRECTORY PROPERTY "generalized_lazy_search_${component}_FOUND" TRUE)
        set_property(DIRECTORY
          PROPERTY "generalized_lazy_search_${component}_INCLUDE_DIRS"
          ${generalized_lazy_search_${component}_INCLUDE_DIRS})
        set_property(DIRECTORY
          PROPERTY "generalized_lazy_search_${component}_DEFINITIONS"
          ${generalized_lazy_search_${component}_DEFINITIONS})
        set_property(DIRECTORY
          PROPERTY "generalized_lazy_search_${component}_LIBRARIES"
          ${generalized_lazy_search_${component}_LIBRARIES})
        set_property(DIRECTORY
          PROPERTY "generalized_lazy_search_${component}_DEPENDENCIES"
          ${generalized_lazy_search_${component}_DEPENDENCIES})

        generalized_lazy_search_traverse_components("${output_variable}" 
          ${generalized_lazy_search_${component}_DEPENDENCIES})

        include("${target_include}")

        # This is not necessary in recent versions of cmake, which supports
        # transitive includes via INTERFACE_INCLUDE_DIRECTORIES.
        foreach(dependency ${generalized_lazy_search_${component}_DEPENDENCIES})
          set_property(DIRECTORY APPEND
            PROPERTY "generalized_lazy_search_${component}_INCLUDE_DIRS"
            ${generalized_lazy_search_${dependency}_INCLUDE_DIRS})
          set_property(DIRECTORY APPEND
            PROPERTY "generalized_lazy_search_${component}_DEFINITIONS"
            ${generalized_lazy_search_${dependency}_DEFINITIONS})
          # LIBRARIES is automatically inherited via INTERFACE_LINK_LIBRARIES.
        endforeach()

        list(APPEND output_components "${component}")
      else()
        set_property(DIRECTORY PROPERTY "generalized_lazy_search_${component}_FOUND" FALSE)
      endif()
    endif()
  endforeach()

  set("${output_variable}" ${output_components} PARENT_SCOPE)
endfunction()

function(generalized_lazy_search_export_variable variable)
  set("${variable}" ${${variable}} CACHE INTERNAL "")
endfunction()

function(generalized_lazy_search_export_property variable)
  get_property(value DIRECTORY PROPERTY "${variable}")
  set("${variable}" ${value} CACHE INTERNAL "")
endfunction()

function(generalized_lazy_search_export_notfound variable)
  set("${variable}" "${variable}-NOTFOUND" CACHE INTERNAL "")
endfunction()

function(generalized_lazy_search_package_init)
  generalized_lazy_search_traverse_components(required_components "${ARGN}")

  set(generalized_lazy_search_INCLUDE_DIRS)
  set(generalized_lazy_search_DEFINITIONS)
  set(generalized_lazy_search_LIBRARIES)

  foreach(component ${ARGN})
    generalized_lazy_search_export_property("generalized_lazy_search_${component}_FOUND")

    if(generalized_lazy_search_${component}_FOUND)
      generalized_lazy_search_export_property("generalized_lazy_search_${component}_INCLUDE_DIRS")
      generalized_lazy_search_export_property("generalized_lazy_search_${component}_DEFINITIONS")
      generalized_lazy_search_export_property("generalized_lazy_search_${component}_LIBRARIES")
      generalized_lazy_search_export_property("generalized_lazy_search_${component}_DEPENDENCIES")

      list(APPEND generalized_lazy_search_INCLUDE_DIRS ${generalized_lazy_search_${component}_INCLUDE_DIRS})
      list(APPEND generalized_lazy_search_DEFINITIONS ${generalized_lazy_search_${component}_DEFINITIONS})
      list(APPEND generalized_lazy_search_LIBRARIES ${generalized_lazy_search_${component}_LIBRARIES})
    else()
      generalized_lazy_search_export_notfound("generalized_lazy_search_${component}_INCLUDE_DIRS")
      generalized_lazy_search_export_notfound("generalized_lazy_search_${component}_DEFINITIONS")
      generalized_lazy_search_export_notfound("generalized_lazy_search_${component}_LIBRARIES")
      generalized_lazy_search_export_notfound("generalized_lazy_search_${component}_DEPENDENCIES")
    endif()
  endforeach()

  generalized_lazy_search_export_variable(generalized_lazy_search_INCLUDE_DIRS)
  generalized_lazy_search_export_variable(generalized_lazy_search_DEFINITIONS)
  generalized_lazy_search_export_variable(generalized_lazy_search_LIBRARIES)
endfunction()

#==============================================================================

# Use find_package_handle_standard_args to generate output. This handles the
# REQUIRED keyword, sets generalized_lazy_search_FOUND, and generates the appropriate STATUS and
# FATAL_ERROR messages. We intentionally do not check generalized_lazy_search_LIBRARIES here
# because it may be empty if the user did not specify any COMPONENTS.
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(generalized_lazy_search
  FOUND_VAR generalized_lazy_search_FOUND
  REQUIRED_VARS generalized_lazy_search_PACKAGE_INCLUDE_DIRS
  HANDLE_COMPONENTS)
